/*

Taken from the following article with little modification: 

Sterratt DC, Groen MR, Meredith RM, van Ooyen A (2012) 
Spine calcium transients induced by synaptically-evoked action potentials
can predict synapse location and establish synaptic democracy 
PLoS Comput Biol 8(6):e1002545

*/

begintemplate Spine

public head, neck
public loc, addSyn, destroy
objref this

create head
create neck
proc init() {
    headL = 0.2
    headdiam = 1
    neckL = 1 
    neckdiam = 0.2
    create head
    create neck
    connect  head(0), neck(1)
    head insert pas
    neck insert pas
    // Negative arg prevents location
    if ($1 >= 0) { this.loc($1) }
    // Get properties of spine from parent (which we're accessing)
    pRa = Ra
	headCm = cm
    if (ismembrane("g_pas")) {
        pg_pas = g_pas
    } else {
        pg_pas = 10E-5
    }
    head {
        Ra = pRa
        g_pas = pg_pas 
        diam = headdiam
        L = headL
		cm = headCm
    }
    neck {
        Ra = pRa
        g_pas = pg_pas 
        diam = neckdiam
        L = neckL
		cm = headCm
    }
}

proc loc() {
    connect neck(0), $1    
}

proc addSyn() {
    head $o1.loc(1)
}

proc destroy() {
    head disconnect()
    neck disconnect()
    head delete_section()
    neck delete_section()
}

endtemplate Spine